buildscript {
    ext.kotlin_version = '1.2.21'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

group 'ru.renue.fts'
version '1.0'

apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'distribution'

mainClassName = 'nlb.manager.app.NlbManagerApplication'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
       url 'https://jitpack.io'
    }
}

distributions {
    main {
        contents {
            into("") {
                from(".\\src\\main\\resources\\application.properties")
            }
        }
    }
}


dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile "com.github.jkcclemens:khttp:-SNAPSHOT"
}

applicationDefaultJvmArgs = ["-Dconfig.location=MY_APP_HOME/application.properties"]

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('MY_APP_HOME', '\$APP_HOME')
        windowsScript.text = windowsScript.text.replace('MY_APP_HOME', '%~dp0..')
        def winScriptFile = file getWindowsScript()
        def winFileText = winScriptFile.text
        winFileText = winFileText.replaceAll('set CLASSPATH=.*', 'rem original CLASSPATH declaration replaced by:\nset CLASSPATH=%APP_HOME%\\\\lib\\\\\\*')
        winScriptFile.text = winFileText
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.6"
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4.1'
}